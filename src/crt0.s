#include "constants/global.h"
	.include "constants/gba_constants.inc"

	.syntax unified

	.global Start

	.text

	.arm

Start: @ 8000000
	b Init

	.include "asm/rom_header.inc"

@ 80000C0
	.word 0

	.global GPIOPortData
GPIOPortData: @ 80000C4
	.short 0

	.global GPIOPortDirection
GPIOPortDirection: @ 80000C6
	.short 0

	.global GPIOPortReadEnable
GPIOPortReadEnable: @ 80000C8
	.short 0

@ 80000CA
	.short 0

@ 80000CC
	.space 0x34

	.int GAME_VERSION
	.int GAME_LANGUAGE

	.ascii "pokemon emerald version"
	.space 9

	.int gMonFrontPicTable
	.int gMonBackPicTable
	.int gMonPaletteTable
	.int gMonShinyPaletteTable
	.int gMonIconTable
	.int gMonIconPaletteIndices
	.int gMonIconPaletteTable
	.int gSpeciesNames
	.int gMoveNames
	.int gDecorations

	.int 0x00001270 @ offsetof(struct SaveBlock1, flags) 
	.int 0x0000139c @ offsetof(struct SaveBlock1, vars)
	.int 0x00000018 @ offsetof(struct SaveBlock2, pokedex)
	.int 0x00000988 @ offsetof(struct SaveBlock1, seen1)
	.int 0x00003b24 @ offsetof(struct SaveBlock1, seen2)
	.int 0x00000046 @ ?
	.int 0x000008e4 @ ?
	.int 0x000008ac @ ?
	.int 0x00000182 @ NATIONAL_DEX_COUNT?

	.byte 0x07, 0x0a, 0x0a, 0x0a, 0x0c, 0x0c, 0x06, 0x0c
	.byte 0x06, 0x10, 0x12, 0x0c, 0x0f, 0x0b, 0x01, 0x08

	.int 0x0000000c @ ?
	.int 0x00000f2c @ sizeof(struct SaveBlock2)
	.int 0x00003d88 @ sizeof(struct SaveBlock1)
	.int 0x00000234 @ offsetof(struct SaveBlock1, playerPartyCount)
	.int 0x00000238 @ offsetof(struct SaveBlock1, playerParty)
	.int 0x00000009 @ offsetof(struct SaveBlock2, specialSaveWarpFlags)
	.int 0x0000000a @ offsetof(struct SaveBlock2, playerTrainerId)
	.int 0x00000000 @ offsetof(struct SaveBlock2, playerName)
	.int 0x00000008 @ offsetof(struct SaveBlock2, playerGender)
	.int 0x00000ca8 @ offsetof(struct SaveBlock2, frontier.challengeStatus)
	.int 0x00000ca8 @ offsetof(struct SaveBlock2, frontier.challengeStatus)
	.int 0x000031c7 @ offsetof(struct SaveBlock1, externalEventFlags)
	.int 0x000031b3 @ offsetof(struct SaveBlock1, externalEventData)
	.int 0x00000000

	.int gBaseStats
	.int gAbilityNames
	.int gAbilityDescriptionPointers
	.int gItems
	.int gBattleMoves
	.int gBallSpriteSheets
	.int gBallSpritePalettes

	.int 0x000000a8 @ offsetof(struct SaveBlock2, gcnLinkFlags)
	.int 0x00000864 @ ?
	.int 0x0000089b @ ?

	.byte 0x1e, 0x1e, 0x10, 0x40

	.int 0x0000322e @ offsetof(struct SaveBlock1, ? part-way into unk_322C)
	.int 0x00000498 @ offsetof(struct SaveBlock1, pcItems)
	.int 0x000031a8 @ offsetof(struct SaveBlock1, giftRibbons)
	.int 0x000031f8 @ offsetof(struct SaveBlock1, enigmaBerry)
	.int 0x00000034 @ offsetof(struct SaveBlock1, mapView)
	.int 0x00000000
	.int 0x00000000

	.arm
	.align 2, 0
	.global Init
Init: @ 8000204
	mov r0, #PSR_IRQ_MODE
	msr cpsr_cf, r0
	ldr sp, sp_irq
	mov r0, #PSR_SYS_MODE
	msr cpsr_cf, r0
	ldr sp, sp_sys
	ldr r1, =INTR_VECTOR
	adr r0, IntrMain
	str r0, [r1]
	.if MODERN
	mov r0, #255 @ RESET_ALL
	svc #1 << 16
	.endif @ MODERN
	ldr r1, =AgbMain + 1
	mov lr, pc
	bx r1
	b Init

	.align 2, 0
sp_sys: .word IWRAM_END - 0x1c0
sp_irq: .word IWRAM_END - 0x60

	.pool

	.arm
	.align 2, 0
	.global IntrMain
IntrMain: @ 8000248
	mov r3, #REG_BASE
	add r3, r3, #OFFSET_REG_IE
	ldr r2, [r3]
	ldrh r1, [r3, #OFFSET_REG_IME - 0x200]
	mrs r0, spsr
	stmfd sp!, {r0-r3,lr}
	mov r0, #0
	strh r0, [r3, #OFFSET_REG_IME - 0x200]
	and r1, r2, r2, lsr #16
	mov r12, #0
	ands r0, r1, #INTR_FLAG_VCOUNT
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	mov r0, 0x1
	strh r0, [r3, #OFFSET_REG_IME - 0x200]
	ands r0, r1, #INTR_FLAG_SERIAL
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_TIMER3
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_HBLANK
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_VBLANK
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_TIMER0
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_TIMER1
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_TIMER2
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_DMA0
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_DMA1
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_DMA2
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_DMA3
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_KEYPAD
	bne IntrMain_FoundIntr
	add r12, r12, 0x4
	ands r0, r1, #INTR_FLAG_GAMEPAK
	strbne r0, [r3, #REG_SOUNDCNT_X - REG_IE]
	bne . @ spin
IntrMain_FoundIntr:
	strh r0, [r3, #OFFSET_REG_IF - 0x200]
	bic r2, r2, r0
	ldr r0, =gSTWIStatus
	ldr r0, [r0]
	ldrb r0, [r0, 0xA]
	mov r1, 0x8
	lsl r0, r1, r0
	orr r0, r0, #INTR_FLAG_GAMEPAK
	orr r1, r0, #INTR_FLAG_SERIAL | INTR_FLAG_TIMER3 | INTR_FLAG_VCOUNT | INTR_FLAG_HBLANK
	and r1, r1, r2
	strh r1, [r3, #OFFSET_REG_IE - 0x200]
	mrs r3, cpsr
	bic r3, r3, #PSR_I_BIT | PSR_F_BIT | PSR_MODE_MASK
	orr r3, r3, #PSR_SYS_MODE
	msr cpsr_cf, r3
	ldr r1, =gIntrTable
	add r1, r1, r12
	ldr r0, [r1]
	stmfd sp!, {lr}
	adr lr, IntrMain_RetAddr
	bx r0
IntrMain_RetAddr:
	ldmfd sp!, {lr}
	mrs r3, cpsr
	bic r3, r3, #PSR_I_BIT | PSR_F_BIT | PSR_MODE_MASK
	orr r3, r3, #PSR_I_BIT | PSR_IRQ_MODE
	msr cpsr_cf, r3
	ldmia sp!, {r0-r3,lr}
	strh r2, [r3, #OFFSET_REG_IE - 0x200]
	strh r1, [r3, #OFFSET_REG_IME - 0x200]
	msr spsr_cf, r0
	bx lr

	.pool

	.align 2, 0 @ Don't pad with nop.
